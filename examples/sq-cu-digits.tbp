05 REM Taken from: https://rosettacode.org/wiki/Sum_of_square_and_cube_digits_of_an_integer_are_primes#Tiny_BASIC
10 REM Sum of square and cube digits of an integer are primes
20 REM N, the number to be tested
30 REM D, the digital sum of its square or cube
40 REM T, temporary variable
50 REM Z, did D test as prime or not
60 LET N = 1
70 LET T = N * N * N
80 GOSUB 200
90 GOSUB 260
100 IF Z = 0 THEN GOTO 160
110 LET T = N * N
120 GOSUB 200
130 GOSUB 260
140 IF Z = 0 THEN GOTO 160
150 PRINT N
160 IF N = 31 THEN END
170 LET N = N + 1
180 GOTO 70
190 REM Calculate sum of digits
200 LET D = 0
210 IF T = 0 THEN RETURN
220 LET D = D + (T - (T / 10) * 10)
230 LET T = T / 10
240 GOTO 210
250 REM Check if is prime
260 LET Z = 0
270 IF D < 2 THEN RETURN
280 LET Z = 1
290 IF D < 4 THEN RETURN
300 LET Z = 0
310 IF (D / 2) * 2 = D THEN RETURN
320 LET T = 1
330 LET T = T + 2
340 IF T * T > D THEN GOTO 370
350 IF (D / T) * T = D THEN RETURN
360 GOTO 330
370 LET Z = 1
380 RETURN
