01 REM From: https://github.com/winny-/tinybasic.rkt/blob/master/tinybasic-examples/examples/pascals-triangle.rkt
02 REM By Winston (Winny) Weinert for his Racket implementation of Tiny BASIC.

80 REM N rows
100 LET N = 10
120 REM ColWidth
140 LET C = 3
150 REM Current Row
160 LET I = 1
170 REM Prefix spaces
180 LET J = C * (N - I)
200 IF J = 0 THEN GOTO 265
205 PRINT " ";
220 LET J = J - 1
240 GOTO 200
250 REM Done with prefix spaces...
260 REM Instantiate Accumulator
265 LET A = 1
270 REM Current element
290 LET J = 1
295 REM Beginning of line loop
300 IF J = 1 THEN GOTO 682
600 REM Pad out the number
602 LET X = A
605 REM Digit count
610 LET Z = 0
612 LET Z = Z + 1
615 LET X = X / 10
620 IF X <> 0 THEN GOTO 612
640 LET Y = (C * 2) - Z
642 GOTO 675
645 PRINT " ";
665 LET Y = Y - 1
675 IF Y <> 0 THEN GOTO 645
682 PRINT A;
690 LET A = A * (I - J) / J
700 LET J = J + 1
750 if J <= I THEN GOTO 300
800 PRINT
850 LET I = I + 1
900 IF I <= N THEN GOTO 180
999 END